#!/usr/bin/env coffee

events = require 'events'
path = require 'path'
glob = require 'glob'
chokidar = require 'chokidar'
flatiron = require 'flatiron'
app = flatiron.app
build = require path.join __dirname, '..', 'cmds', 'build'
test = require path.join __dirname, '..', 'cmds', 'test'
screenshot = require path.join __dirname, '..', 'cmds', 'screenshot'

emitter = new events.EventEmitter

options =
  root: process.cwd()
  libroot: path.join __dirname, '..'
  theme: path.basename process.cwd()
  emitter: emitter
  src: {}
  out: {}
  fixture: {}
  screenshot: {}
  port:
    test: 1334
    screenshot: 1335

options.src.dir = path.join options.root, 'src'
options.src.less = path.join options.src.dir, 'theme.less'
options.src.yaml = path.join options.src.dir, 'theme.yaml'

options.out.dir = options.root
options.out.css = path.join options.out.dir, options.theme + '.css'
options.out.yaml = path.join options.out.dir, options.theme + '.yaml'

options.screenshot.dir = path.join options.root, 'screenshots'
options.fixture.dir = path.join options.libroot, 'fixtures'

app.use flatiron.plugins.log
app.use flatiron.plugins.cli,
  usage: 'limechat-theme-builder build|watch|test'

app.commands.build = (cb) ->
  app.log.info 'Building..'
  build.run options, cb

app.commands.watch = (cb) ->
  app.log.info 'Watching..'
  srcWatcher = chokidar.watch options.src.dir,
    ignored: /^\./
    persistent: true
  srcWatcher.on 'all', (path) ->
    app.log.info 'Source files changed'
    app.commands.build()
  srcWatcher.on 'error', (e) ->
    app.log.error e

  testWatcher = chokidar.watch options.fixture.dir,
    ignored: /^\./
    persistent: true
  testWatcher.add options.out.css
  testWatcher.on 'all', (path) ->
    app.log.info 'Test target changed'
    emitter.emit 'reload'
  testWatcher.on 'error', (e) ->
    app.log.error e

app.commands.test = (cb) ->
  app.commands.build (e) ->
    if e
      app.log.error e
      process.exit 1

    app.commands.watch()
    app.use test.plugin, options
    app.start options.port.test
    app.log.info 'Test fixtures available at http://localhost:' + options.port.test + '/t/'

app.commands.screenshot = (cb) ->
  app.use test.plugin, options
  app.start options.port.screenshot, () ->
    app.log.info 'Screenshot server running at http://localhost:' + options.port.screenshot + '/t/'
    screenshot.run options, () ->
      app.server.close()

app.log.info 'Theme:', options.theme
app.start()
